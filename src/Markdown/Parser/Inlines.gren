module Markdown.Parser.Inlines exposing ( parse, parser )

{-| Inline-level parsers.

@docs parse, parser

-}

import Markdown.Inline exposing ( Html(..), Inline(..) )
import Markdown.Parser.Html as Html
import Maybe.Extra as Maybe
import Parser exposing (..)
import Parser.Extra exposing (..)
import Transmutable.Html as Html
import Transmutable.Html.Interior as Html
import Transmutable.Html.Parser as Html



-- ðŸŒ³


{-|-}
type RawInline
    = RawHtml Html
    | RawHtmlOpening Html.Opening
    | RawHtmlClosing Html.Closing
    | RawHtmlText String
    | RawInline Inline



-- ðŸ› ï¸


{-| Parse inlines.
-}
parse : String -> Result (Array DeadEnd) (Array Inline)
parse =
    Parser.run parser



-- TOP-LEVEL PARSER


{-| Top-level inline parser.
-}
parser : Parser (Array Inline)
parser =
    map rawToInlines rawParser


rawToInlines : Array RawInline -> Array Inline
rawToInlines rawInlines =
    { acc = []
    , remainder = rawInlines
    }
        |> translateRaw
        |> .acc


translateRaw :
    { acc : Array Inline
    , remainder : Array RawInline
    }
    -> { acc : Array Inline
       , remainder : Array RawInline
       , closing : Maybe Html.Closing
       , end : Bool
       }
translateRaw { acc, remainder } =
    when Array.popFirst remainder is
        Just record ->
            translateRawElement acc record.first record.rest

        Nothing ->
            { acc = acc
            , remainder = []
            , closing = Nothing
            , end = True
            }


translateRawElement :
    Array Inline
    -> RawInline
    -> Array RawInline
    -> { acc : Array Inline
       , remainder : Array RawInline
       , closing : Maybe Html.Closing
       , end : Bool
       }
translateRawElement acc rawElement rest =
    let
        merge a mergeAcc =
            Array.pushLast a mergeAcc

        add a =
            translateRaw
                { acc = merge a acc
                , remainder = rest
                }

        nodeCreatorAdapter props inlines =
            InlineHtmlNode
                { attributes = props.attributes
                , tagName = props.tagName
                , inlines = inlines
                }
    in
    when rawElement is
        RawInline inline ->
            add inline

        --
        -- HTML
        --
        RawHtml html ->
            html
                |> InlineHtml
                |> add

        RawHtmlOpening opening ->
            Html.manageOpening
                { appendToPost = merge
                , manageNext = translateRaw
                , nodeCreator = nodeCreatorAdapter
                , textCreator = Text
                }
                { acc = acc
                , remainder = rest
                }
                opening

        RawHtmlClosing closing ->
            Html.manageClosing
                { acc = acc
                , remainder = rest
                }
                closing

        RawHtmlText string ->
            add (Text string)


rawParser : Parser (Array RawInline)
rawParser =
    loop [] (rawParserLoop [])


rawParserLoop : Array (Parser RawInline) -> Array RawInline -> Parser (Step (Array RawInline) (Array RawInline))
rawParserLoop customInlineParsers inlineElements =
    let
        inlines =
            map (\a -> Loop (inlineElements ++ a))

        inline =
            map Array.singleton >> inlines

        lastElement =
            Array.last inlineElements
    in
    Parser.oneOf
        [ -- Check if we reached the end
          map (\_ -> Done inlineElements) end

        , --
          backtrackable (inlineCodeParser inlineElements lastElement)

        , --
          inline (map RawInline (backtrackable (emphasisWithUnderscoreParser lastElement)))
        , inline (map RawInline (backtrackable (emphasisWithStarParser lastElement)))
        , inline (map RawInline (backtrackable hardLineBreakParser))
        , inline (map RawInline (backtrackable imageParser))
        , inline (map RawInline (backtrackable linkParser))
        , inline (map RawInline (backtrackable autolinksParser))
            -- , --
            --   inline (backtrackable (oneOf customInlineParsers))
            --

        , -- HTML
          Html.nonRecursiveNodeParser
            |> withOptionalLeadingSpaces
            |> andThen
                    (\html ->
                        when html is
                            Html.Node node ->
                                succeed
                                    [ RawHtmlOpening
                                        { tagName = node.properties.tagName
                                        , attributes = Html.attributesToDictionary node.attributes
                                        , closed = False
                                        , source = ""
                                        }

                                    , --
                                      (when node.descendants is
                                        Html.Regular array ->
                                            array
                                      )
                                        |> Html.arrayToString
                                        |> Text
                                        |> RawInline

                                    , --
                                      RawHtmlClosing
                                        { tagName = node.properties.tagName
                                        , source = ""
                                        }
                                    ]

                            _ ->
                                problem ""
                    )
            |> backtrackable
            |> inlines


        , --
          Html.nonNodeParser
            |> map RawHtml
            |> inline

        , --
          [ map RawHtmlOpening (backtrackable Html.openingParser)
          , map RawHtmlClosing (backtrackable Html.closingParser)
          ]
            |> oneOf
            |> withOptionalLeadingSpaces
            |> backtrackable
            |> inline

        , --
          textLoopParser inlineElements lastElement
        ]



-- INDIVIDUAL PARSERS


autolinksParser : Parser Inline
autolinksParser =
    oneOf
        [ succeed (\a b c d -> a ++ b ++ c ++ ":" ++ d)
            |. specificChar '<'
            |= getChompedString (chompIf Char.isAlpha)
            |= getChompedString (chompIf isSchemeChar)
            |= getChompedString (chompWhile isSchemeChar)
            |. specificChar ':'
            |= getChompedString (chompUntil ">")
            |. specificChar '>'
            |> andThen
                    (\url ->
                        if String.contains " " url then
                            problem "URL cannot contain spaces"
                        else
                            { text = url
                            , url = url
                            }
                                |> Link
                                |> succeed
                    )
            |> backtrackable

        , -- Email
          succeed (\a b -> a ++ "@" ++ b)
            |. specificChar '<'
            |= getChompedString (chompOneOrMore (\c -> Char.isAlphaNum c || c == '.' || c == '!' || c == '#' || c == '$' || c == '%' || c == '&' || c == '\'' || c == '*' || c == '+' || c == '/' || c == '=' || c == '?' || c == '^' || c == '_' || c == '`' || c == '{' || c == '|' || c == '}' || c == '~' || c == '-' || c == ']' || c == '+'))
            |. specificChar '@'
            |= getChompedString (chompOneOrMore (\c -> Char.isAlphaNum c || c == '.' || c == '-'))
            |. specificChar '>'
            |> map
                    (\email ->
                        Link
                            { text = email
                            , url = "mailto:" ++ email
                            }
                    )
        ]


emphasisParser : Char -> Int -> Maybe RawInline -> Parser Inline
emphasisParser emphasisChar addToLeftFlank lastElement =
    let
        emphasisCharString =
            String.fromArray
                [ emphasisChar
                ]

        flankFromSome s =
            String.fromArray (Array.pushFirst s.match s.rest)

        originalPrecedingChar =
            when lastElement is
                Just (RawInline (Text t)) ->
                    Array.last (String.toArray t)

                _ ->
                    Nothing
    in
    succeed identity
        |= lookAhead (specificAmountOf (addToLeftFlank + 1) (specificChar emphasisChar))
        |> map flankFromSome
        |> andThen
                (\flank ->
                    let
                        chompUntilNextClosing opts =
                            succeed identity
                                |= getChompedString (chompUntil opts.rightFlank)
                                |> andThen (check opts)

                        check opts additionalEmphasisedText =
                            let
                                entireText =
                                    opts.emphasisedText ++ additionalEmphasisedText

                                chars =
                                    String.toArray entireText

                                lastCharPrevious =
                                    if opts.checkOpening then
                                        originalPrecedingChar
                                    else
                                        Array.last (String.toArray opts.emphasisedText)
                            in
                            if
                                -- (1)
                                opts.checkOpening
                                    && isOpening
                                            { precedingChar = originalPrecedingChar
                                            , followingChar = Array.first chars
                                            }
                                    == False
                            then
                                problem "Not a viable left flank"
                            else if opts.nestedChomp then
                                -- (2)
                                oneOf
                                    [ succeed identity
                                        |. symbol opts.rightFlank
                                        |> andThen
                                                (\_ ->
                                                    chompUntilNextClosing
                                                        { opts
                                                            | checkOpening = False
                                                            , emphasisedText = entireText ++ opts.rightFlank
                                                            , nestedChomp = False
                                                            , rightFlank = flank
                                                        }
                                                )
                                        |> backtrackable
                                    , check
                                        { opts
                                            | checkOpening = False
                                            , emphasisedText = entireText
                                            , nestedChomp = False
                                            , rightFlank = flank
                                        }
                                        ""
                                    ]
                            else
                                let
                                    handleClosing maybeAmountOfChars text =
                                        succeed identity
                                            |= (when maybeAmountOfChars is
                                                    Just a ->
                                                        specificAmountOf a (specificChar emphasisChar)

                                                    Nothing ->
                                                        some (specificChar emphasisChar)
                                                )
                                            |> andThen
                                                    (\flankChars ->
                                                        let
                                                            additionalFlank =
                                                                flankFromSome flankChars

                                                            extractRightFlank str fl =
                                                                if String.endsWith emphasisCharString str then
                                                                    extractRightFlank (String.dropLast 1 str) (fl ++ emphasisCharString)
                                                                else
                                                                    { emph = str
                                                                    , fl = fl
                                                                    }

                                                            rightFlankExtraction =
                                                                extractRightFlank chompOpts.emphasisedText additionalFlank

                                                            extractLeftFlank str fl =
                                                                if String.startsWith emphasisCharString str then
                                                                    extractLeftFlank (String.dropFirst 1 str) (fl ++ emphasisCharString)
                                                                else
                                                                    { emph = str
                                                                    , fl = fl
                                                                    }

                                                            leftFlankExtraction =
                                                                extractLeftFlank rightFlankExtraction.emph opts.rightFlank

                                                            encounteredLeftFlank =
                                                                leftFlankExtraction.fl

                                                            encounteredRightFlank =
                                                                rightFlankExtraction.fl

                                                            chompOpts =
                                                                { opts
                                                                    | checkOpening = False
                                                                    , emphasisedText = text
                                                                    , nestedChomp = False
                                                                }

                                                            chompOptsWithFlank =
                                                                { chompOpts | emphasisedText = rightFlankExtraction.emph ++ encounteredRightFlank }

                                                            chompOptsWithFlankNested =
                                                                { chompOptsWithFlank
                                                                    | nestedChomp = True
                                                                    , rightFlank = encounteredRightFlank
                                                                }

                                                            lookAtClosing emphasisedText =
                                                                let
                                                                    lastChar =
                                                                        Array.last (String.toArray emphasisedText)

                                                                    otherEmphasisCharString =
                                                                        if emphasisChar == '*' then
                                                                            "_"
                                                                        else
                                                                            "*"
                                                                in
                                                                succeed identity
                                                                    |= oneOf
                                                                            [ map Just (lookAhead char)
                                                                            , map (\_ -> Nothing) end
                                                                            ]
                                                                    |> andThen
                                                                            (\followingChar ->
                                                                                if
                                                                                    isClosing
                                                                                        { precedingChar = lastChar
                                                                                        , followingChar = followingChar
                                                                                        }
                                                                                then
                                                                                    wrapUp entireText chompOptsWithFlank
                                                                                else if
                                                                                    isOpening
                                                                                        { precedingChar = lastChar
                                                                                        , followingChar = followingChar
                                                                                        }
                                                                                then
                                                                                    chompUntilNextClosing chompOptsWithFlankNested
                                                                                else
                                                                                    chompUntilNextClosing chompOptsWithFlank
                                                                            )
                                                        in
                                                        if encounteredRightFlank == opts.rightFlank && encounteredLeftFlank == opts.rightFlank then
                                                            lookAtClosing rightFlankExtraction.emph
                                                        else
                                                            oneOf
                                                                [ chompUntilNextClosing chompOptsWithFlankNested
                                                                , chompUntilNextClosing chompOptsWithFlank

                                                                , --
                                                                  if flank == additionalFlank then
                                                                    lookAtClosing rightFlankExtraction.emph
                                                                  else
                                                                    problem ""
                                                                ]
                                                    )
                                            |> backtrackable

                                    tryUntilNoClosing r text =
                                        lookAhead (many (specificChar emphasisChar))
                                            |> andThen
                                                    (\emphChars ->
                                                        let
                                                            length =
                                                                Array.length emphChars
                                                        in
                                                        if length - r <= 0 then
                                                            problem ""
                                                        else if length > String.unitLength opts.rightFlank then
                                                            oneOf
                                                                [ succeed identity
                                                                    |= handleClosing (Just (length - r)) text
                                                                    |> identity
                                                                , tryUntilNoClosing (r + 1) text
                                                                ]
                                                        else
                                                            problem ""
                                                    )
                                in
                                -- (3)
                                oneOf
                                    [ handleClosing Nothing entireText

                                    , --
                                      if String.startsWith emphasisCharString entireText then
                                        problem ""
                                      else if String.contains emphasisCharString entireText then
                                        succeed identity
                                            |. specificChar emphasisChar
                                            |> andThen
                                                    (\_ ->
                                                        chompUntilNextClosing
                                                            { opts
                                                                | emphasisedText = entireText ++ emphasisCharString
                                                                , checkOpening = False
                                                                , nestedChomp = False
                                                            }
                                                    )
                                      else if lastCharPrevious /= Just emphasisChar then
                                        tryUntilNoClosing 1 entireText
                                      else
                                        problem ""
                                    ]

                        -- --------- --
                        -- LEFT SIDE --
                        -- --------- --
                        isLeftFlank { precedingChar, followingChar } =
                            let
                                conditionOne =
                                    Maybe.unwrap True (not << isUnicodeSpace) followingChar

                                conditionTwoA =
                                    Maybe.unwrap True (not << isPunctuation) followingChar

                                conditionTwoB =
                                    Maybe.unwrap False isPunctuation followingChar && Maybe.unwrap True (\c -> isUnicodeSpace c || isPunctuation c) precedingChar
                            in
                            conditionOne && (conditionTwoA || conditionTwoB)

                        isOpening opts =
                            if emphasisChar == '_' then
                                (&&)
                                    (isLeftFlank opts)
                                    (when opts.precedingChar is
                                        Just c ->
                                            isUnicodeSpace c || isPunctuation c

                                        Nothing ->
                                            True
                                    )
                            else
                                isLeftFlank opts

                        -- ---------- --
                        -- RIGHT SIDE --
                        -- ---------- --
                        isRightFlank { precedingChar, followingChar } =
                            let
                                conditionOne =
                                    Maybe.unwrap True (not << isUnicodeSpace) precedingChar

                                conditionTwoA =
                                    Maybe.unwrap True (not << isPunctuation) precedingChar

                                conditionTwoB =
                                    Maybe.unwrap False isPunctuation precedingChar && Maybe.unwrap True (\c -> isUnicodeSpace c || isPunctuation c) followingChar
                            in
                            conditionOne && (conditionTwoA || conditionTwoB)

                        isClosing opts =
                            if emphasisChar == '_' then
                                -- Disallow intraword emphasis when enphasis char is underscore
                                (&&) (isRightFlank opts) (Maybe.unwrap True (\c -> isUnicodeSpace c || isPunctuation c) opts.followingChar)
                            else
                                isRightFlank opts

                        -- ---- --
                        -- MISC --
                        -- ---- --
                        times =
                            String.unitLength flank

                        wrapUp emphasisedText chomptOpts =
                            when emphasisedText is
                                "" ->
                                    problem "Nothing to emphasise"

                                _ ->
                                    let
                                        parseResult =
                                            emphasisedText
                                                |> parse
                                                |> Result.withDefault []

                                        otherEmphasisCharString =
                                            if emphasisChar == '*' then
                                                "_"
                                            else
                                                "*"
                                    in
                                    when
                                        { parseResult = parseResult
                                        , anyExcessOpeningEmphasis =
                                            when Array.first parseResult is
                                                Just (Text t) ->
                                                    String.startsWith emphasisCharString t

                                                _ ->
                                                    False
                                        , anyExcessClosingEmphasis =
                                            when Array.last parseResult is
                                                Just (Text t) ->
                                                    String.endsWith emphasisCharString t

                                                _ ->
                                                    False
                                        , endsWithInlineCodeChar =
                                            when Array.last parseResult is
                                                Just (Text t) ->
                                                    String.endsWith "`" t

                                                _ ->
                                                    False
                                        }
                                    is
                                        { anyExcessOpeningEmphasis = True } ->
                                            problem ""

                                        { anyExcessClosingEmphasis = True } ->
                                            problem ""

                                        { endsWithInlineCodeChar = True } ->
                                            chompUntilNextClosing chomptOpts

                                        { parseResult = [ Emphasis record ] } ->
                                            if record.char == emphasisChar then
                                                Emphasis
                                                    { char = emphasisChar
                                                    , times = times + record.times
                                                    , inlines = record.inlines
                                                    }
                                                |> succeed
                                            else
                                                Emphasis
                                                    { char = emphasisChar
                                                    , times = times
                                                    , inlines = parseResult
                                                    }
                                                |> succeed

                                        _ ->
                                            -- Not sure what the correct solution should be yet
                                            Emphasis
                                                { char = emphasisChar
                                                , times = times
                                                , inlines = parseResult
                                                }
                                            |> succeed

                    in
                    oneOf
                        [ succeed identity
                            |. symbol flank
                            |. lookAhead (chompUntil flank)
                            |> andThen
                                    (\_ ->
                                        chompUntilNextClosing
                                            { checkOpening = True
                                            , rightFlank = flank
                                            , emphasisedText = ""
                                            , nestedChomp = False
                                            }
                                    )
                            |> backtrackable
                        , emphasisParser emphasisChar (addToLeftFlank + 1) lastElement
                        ]
                )


emphasisWithStarParser : Maybe RawInline -> Parser Inline
emphasisWithStarParser =
    emphasisParser '*' 0


emphasisWithUnderscoreParser : Maybe RawInline -> Parser Inline
emphasisWithUnderscoreParser =
    emphasisParser '_' 0


hardLineBreakParser : Parser Inline
hardLineBreakParser =
    succeed identity
        |. specificChar ' '
        |. specificChar ' '
        |. many (specificChar ' ')
        |. specificChar '\n'
        |> map (\_ -> HardLineBreak)


imageParser : Parser Inline
imageParser =
    succeed identity
        |. symbol "!["
        |= getChompedString (chompUntil "]")
        |. specificChar ']'
        |> andThen
                (\alt ->
                    -- TODO: Support image titles
                    succeed identity
                        |. specificChar '('
                        |= getChompedString (chompUntil ")")
                        |. specificChar ')'
                        |> map
                                (\url ->
                                    Image
                                        { alt = alt
                                        , url = url
                                        }
                                )
                )


inlineCodeParser : Array RawInline -> Maybe RawInline -> Parser (Step (Array RawInline) (Array RawInline))
inlineCodeParser inlineElements lastElement =
    succeed
        (\o s ->
            { opening = o ++ "`" ++ s
            , closing = s ++ o ++ "`"
            }
        )
        |. specificChar '`'
        |= getChompedString (chompWhile (\c -> c == '`'))
        |= oneOf
                [ getChompedString (symbol " ")
                , getChompedString (symbol "\n")
                , succeed ""
                ]
        |> andThen
                (\{ opening, closing } ->
                    let
                        chompUntilClosing close code =
                            succeed identity
                                |= getChompedString (chompUntilEndOr close)
                                |. symbol close
                                |> backtrackable
                                |> andThen (checkClosing close code)

                        checkClosing close code additionalCode =
                            let
                                allCode =
                                    code ++ additionalCode
                            in
                            succeed identity
                                |= oneOf
                                        [ map (\_ -> True) (specificChar '`')
                                        , succeed False
                                        ]
                                |> andThen
                                        (\bool ->
                                            if bool then
                                                -- problem "Closing did not match"
                                                chompUntilClosing close (allCode ++ closing ++ "`")
                                            else
                                                succeed allCode
                                        )

                        addToBlocks code =
                            let
                                finalCode =
                                    code
                                        |> (\c ->
                                                if code == "" && String.endsWith " " opening then
                                                    c ++ " "
                                                else
                                                    c
                                            )
                                        |> (\c ->
                                                if code == "" && String.startsWith " " closing then
                                                    c ++ " "
                                                else
                                                    c
                                            )
                                        |> String.replace "\n" " "
                            in
                            Loop (Array.pushLast (RawInline (InlineCode finalCode)) inlineElements)
                    in
                    oneOf
                        [ -- Either it's actual inline code:
                          -- (a) with some or no whitespace on both ends
                          map addToBlocks (chompUntilClosing closing "")

                        , -- (b) with some whitespace at the beginning, but not the end
                          map (\c -> addToBlocks (" " ++ c)) (chompUntilClosing (String.trimLeft closing) "")

                        , -- Or the opening is text
                          succeed (textLoop inlineElements lastElement opening)
                        ]
                )


linkParser : Parser Inline
linkParser =
    succeed identity
        |. specificChar '['
        |= getChompedString (chompUntil "]")
        |. specificChar ']'
        |> andThen
                (\text ->
                    if String.contains "`" text then
                        problem "Inline code has precedence"
                    else
                        -- TODO: Support link titles
                        succeed identity
                            |. specificChar '('
                            |= getChompedString (chompUntil ")")
                            |. specificChar ')'
                            |> andThen
                                    (\url ->
                                        if String.contains "`" url then
                                            problem "Inline code has precedence"
                                        else
                                            { text = text
                                            , url = url
                                            }
                                                |> Link
                                                |> succeed
                                    )
                )


{-| Parses one character at a time, so that it can try the other inline parsers
after each character. Concatenates `Text` automatically.
-}
textLoopParser : Array RawInline -> Maybe RawInline -> Parser (Step (Array RawInline) (Array RawInline))
textLoopParser inlineElements lastElement =
    succeed (textLoop inlineElements lastElement) |= getChompedString chompOne



-- ãŠ™ï¸


isSchemeChar : Char -> Bool
isSchemeChar c =
    Char.isAlphaNum c || c == '+' || c == '.' || c == '-'


textLoop : Array RawInline -> Maybe RawInline -> String -> Step (Array RawInline) (Array RawInline)
textLoop inlineElements lastElement text =
    when text is
        "" ->
            Done inlineElements

        _ ->
            when lastElement is
                Just (RawInline (Text previousText)) ->
                    Loop (Array.pushLast (RawInline (Text (previousText ++ text))) (Array.dropLast 1 inlineElements))

                _ ->
                    Loop (Array.pushLast (RawInline (Text text)) inlineElements)
