module Markdown.Transmutationist exposing ( toHtml, toHtmlWithOptions, inlineToHtml, inlineToHtmlWithOptions, LoadingStrategy(..), loadingStrategyToString )

{-| Markdown transformations.

@docs toHtml, toHtmlWithOptions, inlineToHtml, inlineToHtmlWithOptions

## Loading Strategy

@docs LoadingStrategy, loadingStrategyToString

-}

import Dict
import Markdown.Block exposing ( Block(..) )
import Markdown.Inline exposing ( Html(..), Inline(..) )
import Transmutable.Html as Html exposing ( Html, text )
import Transmutable.Html.Attributes as A


{-|-}
type LoadingStrategy
    = Eager
    | Lazy


{-| Transform a Markdown block into HTML.

This function uses the default options:
- `imageLoadingStrategy`: `eager`, the browser default.

-}
toHtml : Block -> Html msg
toHtml block =
    toHtmlWithOptions
        block
        { imageLoadingStrategy = defaultImageLoadingStrategy
        }


{-| Transform a Markdown block into HTML, with options.
-}
toHtmlWithOptions :
    Block
    -> { imageLoadingStrategy : LoadingStrategy
       }
    -> Html msg
toHtmlWithOptions block options =
    case block of
        BlockQuote blocks ->
            Html.blockquote [] (Array.map toHtml blocks)

        CodeBlock { body, language } ->
            Html.pre
                []
                [ Html.code
                    (case language of
                        Just l ->
                            [ A.class ("language-" ++ l)
                            ]

                        Nothing ->
                            []
                    )
                    [ text body
                    ]
                ]

        Heading { level } inlines ->
            (case level of
                1 ->
                    Html.h1

                2 ->
                    Html.h2

                3 ->
                    Html.h3

                4 ->
                    Html.h4

                5 ->
                    Html.h5

                6 ->
                    Html.h6

                _ ->
                    Html.p
            )
                []
                (Array.map inlineToHtml inlines)

        HtmlBlock html ->
            inlineMarkdownHtmlToHtml html

        HtmlBlockNode props blocks ->
            htmlNode props (Array.map toHtml blocks)

        Inline inline ->
            inlineToHtmlWithOptions
                inline
                { imageLoadingStrategy = options.imageLoadingStrategy
                }

        OrderedLooseList { start } listItems ->
            Html.ol
                [ A.attribute "start" (String.fromInt start)
                ]
                (Array.map (\blocks -> Html.li [] (Array.map toHtml blocks)) listItems)

        OrderedTightList { start } listItems ->
            Html.ol
                [ A.attribute "start" (String.fromInt start)
                ]
                (Array.map (\inlines -> Html.li [] (Array.map inlineToHtml inlines)) listItems)

        Paragraph inlines ->
            Html.p [] (Array.map inlineToHtml inlines)

        ThematicBreak ->
            Html.hr [] []

        UnorderedLooseList listItems ->
            Html.ul [] (Array.map (\blocks -> Html.li [] (Array.map toHtml blocks)) listItems)

        UnorderedTightList listItems ->
            Html.ul [] (Array.map (\inlines -> Html.li [] (Array.map inlineToHtml inlines)) listItems)


{-| Transform an inline Markdown element into HTML.

This function uses the default options:
- `imageLoadingStrategy`: `eager`, the browser default.

-}
inlineToHtml : Inline -> Html msg
inlineToHtml inline =
    inlineToHtmlWithOptions
        inline
        { imageLoadingStrategy = defaultImageLoadingStrategy
        }


{-| Transform an inline Markdown element into HTML, with options.
-}
inlineToHtmlWithOptions :
    Inline
    -> { imageLoadingStrategy : LoadingStrategy
       }
    -> Html msg
inlineToHtmlWithOptions inline { imageLoadingStrategy } =
    case inline of
        Emphasis { times } inlines ->
            (case times of
                1 ->
                    Html.em

                _ ->
                    Html.strong
            )
                []
                (Array.map inlineToHtml inlines)

        HardLineBreak ->
            Html.br [] []

        InlineCode code ->
            Html.code
                []
                [ text code
                ]

        InlineHtml html ->
            inlineMarkdownHtmlToHtml html

        InlineHtmlNode props inlines ->
            htmlNode props (Array.map inlineToHtml inlines)

        Image { alt, url } ->
            Html.img
                [ A.alt alt
                , A.src url
                , A.attribute "loading" (loadingStrategyToString imageLoadingStrategy)
                ]
                []

        Link props ->
            Html.a
                [ A.href props.url
                ]
                [ text props.text
                ]

        Text t ->
            text t



-- ğŸ› ï¸


{-|-}
loadingStrategyToString : LoadingStrategy -> String
loadingStrategyToString strat =
    case strat of
        Eager ->
            "eager"

        Lazy ->
            "lazy"



-- ãŠ™ï¸


htmlNode { tagName, attributes } =
    Html.node
        tagName
        (attributes
            |> Dict.toArray
            |> Array.map (\{ key, value } -> A.attribute key value)
        )


inlineMarkdownHtmlToHtml : Markdown.Inline.Html -> Html.Html msg
inlineMarkdownHtmlToHtml html =
    case html of
        Cdata s ->
            Html.cdata s

        Comment s ->
            Html.comment s

        Declaration s ->
            Html.declaration s

        ProcessingInstruction s ->
            Html.processingInstruction s



-- ãŠ™ï¸


defaultImageLoadingStrategy =
    Eager
